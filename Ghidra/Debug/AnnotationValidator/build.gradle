apply from: "${rootProject.projectDir}/gradle/javaProject.gradle"
apply from: "${rootProject.projectDir}/gradle/jacocoProject.gradle"
apply from: "${rootProject.projectDir}/gradle/javaTestProject.gradle"
apply from: "${rootProject.projectDir}/gradle/distributableGhidraModule.gradle"

apply plugin: 'eclipse'
eclipse.project.name = 'Debug AnnotationValidator'

dependencies {
	compile project(':ProposedUtils')
}

// no-dep jar for experiments in loading annotation processor into Eclipse
def boolean filterJar(File jarfile) {
	if (jarfile.name.contains("gradle-api")) {
		return false
	} else if (jarfile.name.contains("groovy-all")) {
		return false
	} else if (jarfile.name.contains("gradle-installation-beacon")) {
		return false
	}
	return true
}

task configureNodepJar {
	doLast {
		configurations.runtime.files.forEach {
			if (filterJar(it)) {
				nodepJar.from(zipTree(it)) {
					exclude("META-INF/**")
				}
			}
		}
	}
}

task nodepJar(type: Jar) {
	inputs.file(file(jar.archivePath))
	dependsOn(configureNodepJar)
	dependsOn(jar)

	appendix = 'nodep'

	from(zipTree(jar.archivePath))
}
